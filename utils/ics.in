#!@GUILE@ \
-e main
!#

;;; ics.in -- Get various information from iCalendars.

;; Copyright (C) 2016-2022 Artyom V. Poptsov <poptsov.artyom@gmail.com>
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.
;;
;; The program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with the program.  If not, see <http://www.gnu.org/licenses/>.


;;; Commentary:


;;; Code:

(use-modules (ice-9 getopt-long)
             ((ice-9 pretty-print)
              #:renamer (symbol-prefix-proc 'ice-9:))
             (srfi srfi-41)
             (ics)
             (ics version)
             (ics object)
             (ics common)
             (ics conv))

(define (print-version)
  (format #t "ics ~a~%" (ics-version/string))
  (display "\
Copyright (C) 2016-2022 Artyom V. Poptsov <poptsov.artyom@gmail.com>
License GPLv3+: GNU GPL version 3 or later <https://gnu.org/licenses/gpl.html>.
This is free software: you are free to change and redistribute it.
There is NO WARRANTY, to the extent permitted by law.

Written by Artyom V. Poptsov.
"))

(define (print-help-and-exit)
  (display "\
Usage ics [options]

The default behaviour of the program is to read iCalendar stream from
stdin and handle it according to the specified options.

Options:
  --help, -h                 Print this message and exit.
  --version                  Print Guile-ICS version.
  --pretty-print, -p <indent>
                             Pretty-print iCalendar stream to the
                             stdout.
  --to-org-mode              Convert iCalendar data to org-mode format.
")
  (exit))


;;; Option spec.

(define %option-spec
  '((help         (single-char #\h) (value #f))
    (version                        (value #f))
    (to-org-mode                    (value #f))
    (pretty-print (single-char #\p) (value optional))))

(ics-debug-set! #f)


;;; Entry point.

(define (handle-pretty-print indent)
  (let ((l (ics->stream)))
    (stream-for-each (lambda (e)
                       (ics-pretty-print e #:indent indent))
                     l)))

(define (handle-to-org-mode)
  (stream-for-each (lambda (ics-object)
                     (ics-object->org-mode ics-object))
                   (ics->stream)))

(define (main args)
  (let* ((options      (getopt-long args %option-spec))
         (help-needed? (option-ref options 'help #f))
         (version-needed? (option-ref options 'version #f))
         (to-org-mode  (option-ref options 'to-org-mode #f))
         (pretty-print (option-ref options 'pretty-print #f)))

    (when version-needed?
      (print-version)
      (exit 0))

    (cond
     (pretty-print
      (let ((indent (if (boolean? pretty-print)
                        2
                        (string->number pretty-print))))
        (handle-pretty-print indent)))
     (to-org-mode
      (handle-to-org-mode))
     (#t
      (print-help-and-exit)))))

;; Local Variables:
;; mode: Scheme
;; End:

;;; ics.in ends here.
